plugins {
    id 'java'
}

repositories {
    mavenLocal()
    maven { url 'https://jitpack.io' }
    mavenCentral()
    maven {
        url 'https://repo.runelite.net'
        content {
            // only download packages for group net.runelite*
            includeGroupByRegex "net\\.runelite.*"
        }
    }
}

def lastSupportedVersion = "1.9.7"
def runeLiteVersion = getRuneLiteVersion()
def supportedCheck = false
if (supportedCheck) {
    if (runeLiteVersion != lastSupportedVersion) {
        println("Warning - client has been updated")
        throw new Exception("Out of date (last supported: " + lastSupportedVersion + " current client: " + runeLiteVersion + ")")
    }
}

static def getRuneLiteVersion() {
    URL url = new URL("http://repo.runelite.net/net/runelite/client/maven-metadata.xml")
    URLConnection urlConnection = url.openConnection()
    BufferedReader bufferedReader = new BufferedReader(new InputStreamReader(
            urlConnection.getInputStream()))
    String latestName = null
    String inputLine
    while ((inputLine = bufferedReader.readLine()) != null) {
        inputLine = inputLine.trim()
        if (inputLine.contains("<release>")) {
            latestName = inputLine.replace("<release>", "").replace("</release>", "")
        }
    }
    bufferedReader.close()
    return latestName
}

dependencies {
    implementation group: 'org.clojure', name: 'clojure', version: '1.9.0'
    implementation group: 'org.clojure', name: 'tools.nrepl', version: '0.2.12'
    implementation group: 'net.runelite', name: 'client', version: runeLiteVersion
    implementation group: 'net.runelite', name: 'cache', version: runeLiteVersion
    implementation 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.slf4j:slf4j-simple:1.7.36'
    testImplementation group: 'net.runelite', name: 'client', version: runeLiteVersion, {
        exclude group: 'ch.qos.logback', module: 'logback-classic'
    }

    implementation 'com.github.OSRSB:OSRSBPlugin:main-SNAPSHOT'
    implementation group: 'com.github.joonasvali.naturalmouse', name: 'naturalmouse', version: '2.0.3'
    implementation group: 'javassist', name: 'javassist', version: '3.12.1.GA'
    implementation group: 'net.sf.jopt-simple', name:'jopt-simple', version: '5.0.4'
}

sourceSets {
    main {
        java {
            srcDirs= ["src/main/java"]
        }
    }
}

project.configurations.implementation.setCanBeResolved(true)
jar {
    manifest.attributes('Main-Class': 'net.runelite.rsb.botLauncher.Application')
    from{
        configurations.implementation.collect {it.isDirectory() ? it: zipTree(it)}
    }

    exclude 'META-INF/*.RSA'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    destinationDirectory.set(file("$rootDir"))
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task botRun(type: JavaExec) {
    group = "Execution"
    description = "Runs the main method within RSB"
    classpath = sourceSets.main.runtimeClasspath + sourceSets.main.compileClasspath
    getMainClass() set "net.runelite.rsb.botLauncher.Application"
    jvmArgs "-debug"
    args "--bot-runelite --developer-mode".split(" ")
}

task headlessBotRun(type: JavaExec) {
    group = "Execution"
    description = "Runs the main method within RSB"
    classpath = sourceSets.main.runtimeClasspath + sourceSets.main.compileClasspath
    getMainClass() set "net.runelite.rsb.botLauncher.Application"
    jvmArgs "-debug"
    args "--bot-runelite --developer-mode --headless".split(" ")
}
